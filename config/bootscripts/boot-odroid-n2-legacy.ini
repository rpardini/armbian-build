ODROIDN2-UBOOT-CONFIG

echo "Hardcoded legacy kernel configuration"
setenv bootlogo "false"
setenv uartconsole "ttyS0,115200n8"
setenv kernelimage "zImage"
setenv condev "console=tty1 console=${uartconsole} loglevel=7"   # on both

# Auto Detection of Monitor settings based on your Screen information
setenv display_autodetect "true"
setenv hdmimode "1080p60hz"
setenv overscan "100"
setenv voutmode "hdmi"
setenv disablehpd "false"
# max cpu frequency in MHz unit
if test "${variant}" = "n2_plus"; then
	setenv fdtfile meson-g12b-odroid-n2-plus.dtb
	setenv max_freq_a73 "2208"
	setenv max_freq_a53 "1908"
else
	setenv fdtfile meson-g12b-odroid-n2.dtb
	setenv max_freq_a73 "1800"
	setenv max_freq_a53 "1896"
fi
setenv maxcpus "6"
if test "${display_autodetect}" = "true"; then hdmitx edid; fi
if test "${bootlogo}" = "true"; then setenv bootsplash "bootsplash.bootfile=bootsplash.armbian"; fi
if test "${hdmimode}" = "custombuilt"; then setenv cmode "modeline=${modeline}"; fi

# Read armbianEnv.txt
if ext4load mmc ${devno}:1 0x44000000 /boot/armbianEnv.txt || fatload mmc ${devno}:1 0x44000000 armbianEnv.txt || ext4load mmc ${devno}:1 0x44000000 armbianEnv.txt; then env import -t 0x44000000 ${filesize}; fi
setenv bootargs "root=${rootdev} rootwait rootfstype=${rootfstype} ${condev} ${amlogic} no_console_suspend fsck.repair=yes net.ifnames=0 elevator=noop hdmimode=${hdmimode} cvbsmode=576cvbs max_freq_a53=${max_freq_a53} max_freq_a73=${max_freq_a73} maxcpus=${maxcpus} voutmode=${voutmode} ${cmode} disablehpd=${disablehpd} ${bootsplash} cvbscable=${cvbscable} overscan=${overscan} consoleblank=0"
echo "Legacy bootargs: ${bootags}"
# Set load addresses
setenv dtb_loadaddr "0x1000000"
setenv k_addr "0x1100000"
setenv loadaddr "0x1B00000"
setenv initrd_loadaddr "0x3700000"

# Load kernel, dtb and initrd
ext4load mmc ${devno}:1 ${k_addr} ${kernelimage} || ext4load mmc ${devno}:1 ${k_addr} boot/${kernelimage}
ext4load mmc ${devno}:1 ${dtb_loadaddr} dtb/amlogic/${fdtfile} || ext4load mmc ${devno}:1 ${dtb_loadaddr} boot/dtb/amlogic/${fdtfile}
ext4load mmc ${devno}:1 ${initrd_loadaddr} uInitrd || ext4load mmc ${devno}:1 ${initrd_loadaddr} boot/uInitrd

fdt addr ${dtb_loadaddr}
# unzip the kernel
unzip ${k_addr} ${loadaddr}
# boot legacy kernel
booti ${loadaddr} ${initrd_loadaddr} ${dtb_loadaddr}
